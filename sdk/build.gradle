apply plugin: 'com.android.library'
apply plugin: 'jacoco'


task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*', '**/Lambda$*.class', '**/Lambda.class', '**/*Lambda.class', '**/*Lambda*.class', '**/*Lambda*.*', '**/*Builder.*', '**/*_MembersInjector.class', '**/*_MembersInjector.*', '**/*_*Factory*.*', '**/*Component*.*', '**/*Module*.*']
    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories = files([mainSrc, "${project.projectDir}/../airlock-sdk-common/src/main/java"])
    classDirectories = files([debugTree, "${project.projectDir}/../airlock-sdk-common/build/classes/main"])
    executionData = fileTree(dir: "$buildDir", includes: [
            "**/*.exec",
            "**/*.ec"
    ])
}


allprojects {
    repositories {
        jcenter()
        maven {
            url "https://maven.google.com"
        }
    }
}

buildscript {
    repositories {
        jcenter()
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.2'
        classpath 'com.android.tools.build:gradle:3.2.1'
    }
}


android {
    compileSdkVersion 27
    buildToolsVersion '27.0.1'

    configurations {
        javadocDeps
    }


    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath = true
            }
        }

    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled false
        }
    }

    testOptions {
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
        unitTests.returnDefaultValues = true

    }

    lintOptions {
        abortOnError false
    }
    sourceSets {
        androidTest {
            //java.srcDir project(':airlock-sdk-common').file("src/main/com/github/**")
            //java.srcDir project(':airlock-sdk-common').file("src/main/com/ibm/airlock/common/test/**")
            //assets.srcDirs project(':airlock-sdk-common').file("src/main/java/com/ibm/airlock/common/test/data")
        }
        test {
            //java.srcDir project(':airlock-sdk-common').file("src/test/java")
        }
    }

}

task copyResDirectoryToClasses(type: Copy) {
    from "${projectDir}/src/main/res/raw"
    into "${buildDir}/intermediates/classes/test/debug/res/raw"
}


dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    provided 'com.android.support:appcompat-v7:27.1.1'
    provided 'org.jetbrains:annotations:16.0.3'
    provided 'com.google.code.findbugs:annotations:3.0.1'
    provided 'org.codehaus.sonar-plugins:sonar-ant-task:2.2'
    provided 'org.jetbrains:annotations-java5:15.0'
    provided 'com.google.android:annotations:4.1.1.4'
    provided 'org.mozilla:rhino:1.7.7.1'
    provided 'com.squareup.okhttp3:okhttp:3.9.1'
    provided 'com.squareup.okhttp3:okhttp-urlconnection:3.9.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'org.mockito:mockito-core:1.9.5'
    androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestCompile 'com.google.android:annotations:4.1.1.4'
    javadocDeps 'com.android.support:appcompat-v7:27.1.1'
    javadocDeps 'com.squareup.okhttp3:okhttp:3.9.1'
    javadocDeps 'com.squareup.okhttp:okhttp-urlconnection:3.9.1'
    javadocDeps 'com.google.code.findbugs:annotations:3.0.1'
    javadocDeps group: 'org.mozilla', name: 'rhino', version: '1.7.7.1'
    javadocDeps group: 'org.jetbrains', name: 'annotations', version: '16.0.3'
    testCompile 'junit:junit:4.12'
    testCompile 'org.json:json:20171018'
    testCompile 'com.google.android:annotations:4.1.1.4'
    testCompile 'org.testng:testng:6.13.1'
    testCompile 'com.squareup.okhttp3:okhttp:3.9.1'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'com.squareup.okhttp3:okhttp-urlconnection:3.9.1'
    testCompile 'org.robolectric:robolectric:3.8'
    testCompile 'org.khronos:opengl-api:gl1.1-android-2.1_r1'
    testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: '1.7.4'
    testCompile group: 'org.powermock', name: 'powermock-api-mockito2', version: '1.7.4'
    androidTestCompile 'com.squareup.okhttp3:okhttp:3.9.1'
    androidTestCompile 'com.squareup.okhttp3:okhttp-urlconnection:3.9.1'

    //dagger 2
    compile 'com.google.dagger:dagger:2.14.1'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.14.1'


    //support libraries with dagger 2
    compile 'com.google.dagger:dagger-android-support:2.14.1'

}


gradle.taskGraph.whenReady {
    gradle.taskGraph.allTasks.each {
        if (it.project == 'sdkSample') {
            it.onlyIf { false }
        }
    }
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.javadocDeps
    println android.getBootClasspath()
    destinationDir = file("$buildDir/javadoc/")
    failOnError false
}

//ignore all test failures - is done temporary, the final solution is test categorization
project.gradle.taskGraph.whenReady {
    ->
    project.tasks.findAll {
        it.name =~ /connected.+AndroidTest/ ||
                it.name == "testDebugUnitTest" ||
                it.name == "testReleaseUnitTest"
    }.each {
        it.ignoreFailures = true
    }
}
